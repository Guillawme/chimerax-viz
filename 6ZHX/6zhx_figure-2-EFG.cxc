# Load map and model
open emdb:11220 6zhx 1zla

# Align 1zla to 6zhx based on chain G
mmaker #3/g to #2/g
view #1

# Define more convenient names for component chains
name H3 /a/e
name H4 /b/f
name H2A /c/g
name H2B /d/h
name histones /a/e/b/f/c/g/d/h
name DNA /i/j
name ALC1 #2/k,l
name mainALC1 #2/k
name LANA #3/k
name LANAinteractions #3/g:1061,1064,1090,1092
name APM #2/g:61,64,90,92#2/h:102,106
name interactions #2/k#2/g:61,64,90,92#2/h:102,106

# Apply color code
color H3 blue
color H4 green
color H2A yellow
color H2B red
color DNA grey
color ALC1 hotpink
color LANA turquoise

hide #3 models
hide atoms

# Orient for starting frame
turn x 50
turn z -110
turn x 10
# These last rotations bring chain K to the front
turn y 180
turn x 5

# Use nice display settings
set bgColor white
lighting soft
graphics silhouettes false
volume #1 level 0.0075

# Figure 2E: map overview

color zone #1 near #2 distance 12

save 6zhx_figure-2E.png supersample 3 transparentbackground true

# Figure 2F1: model overview

hide #1
show #2
hide atoms
show DNA cartoons
show histones cartoons
show ALC1 atoms
style ALC1 stick
color ALC1 byhetero
hide H
graphics silhouettes true

save 6zhx_figure-2F1.png supersample 3 transparentbackground true

# Set up for inset F2
hide #1
show #2
hide atoms
hide cartoons
hide surfaces
show DNA cartoons
show histones surfaces
show ALC1 atoms
hide H
style ALC1 sphere
size atomradius 1.3
graphics silhouettes true

# Re-apply color code (surfaces did not have it)
color H3 blue
color H4 green
color H2A yellow
color H2B red
color DNA grey
color ALC1 hotpink
color ALC1 byhetero

# Prepare zoom-in by saving current view and destination view
view name overview
view mainALC1
view name mainALC1view
view overview

# Part 2: model
# Show peptide as sticks to see acidic patch cavity more easily
style ALC1 stick
size stickradius 0.3

# Show density around peptide
graphics silhouettes false
surface zone #1 nearatoms mainALC1 distance 1.6
show #1
view mainALC1
color #1 turquoise transparency 75

# Re-orient
turn y 30
turn x 30

save 6zhx_figure-2F2.png supersample 3 transparentbackground true

# Set up for inset F3
hide #1
hide surface
show histones cartoons
graphics silhouettes true
show interactions atoms
style interactions stick
size stickradius 0.3
color interactions byhetero
hide HC
hbond mainALC1 reveal true
view mainALC1
clip far 2

save 6zhx_figure-2F3.png supersample 3 transparentbackground true

# Set up for panel G
show histones cartoons
graphics silhouettes true
show interactions atoms
style interactions stick
size stickradius 0.3
color interactions byhetero
show #3 models
show LANAinteractions atoms
style LANAinteractions stick
color LANAinteractions byhetero
show LANA atoms
style LANA stick
size stickradius 0.3
color LANA byhetero
hide H

save 6zhx_figure-2G.png supersample 3 transparentbackground true
